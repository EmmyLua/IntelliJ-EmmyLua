/*
 * Copyright (c) 2017. tangzx(love.tangzx@qq.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

{
  parserClass="com.tang.intellij.lua.parser.LuaParser"

  implements="com.tang.intellij.lua.psi.LuaPsiElement"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Lua"
  psiImplClassSuffix="Impl"
  psiPackage="com.tang.intellij.lua.psi"
  psiImplPackage="com.tang.intellij.lua.psi.impl"
  psiImplUtilClass="com.tang.intellij.lua.psi.LuaPsiImplUtil"

  parserUtilClass="com.tang.intellij.lua.psi.LuaParserUtil"
  elementTypeHolderClass="com.tang.intellij.lua.psi.LuaTypes"
  elementTypeClass="com.tang.intellij.lua.psi.LuaElementType"
  elementTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinition.createType"
  tokenTypeClass="com.tang.intellij.lua.psi.LuaTokenType"
  tokenTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinition.createToken"
  parserImports = "static com.intellij.lang.WhitespacesBinders.*"

  tokens = [
        /* Keywords */
        AND = "and"
        BREAK = "break"
        DO = "do"
        ELSE = "else"
        ELSEIF = "elseif"
        END = "end"
        FALSE = "false"
        FOR = "for"
        FUNCTION = "function"
        IF = "if"
        IN = "in"
        LOCAL = "local"
        NIL = "nil"
        NOT = "not"
        OR = "or"
        REPEAT = "repeat"
        RETURN = "return"
        THEN = "then"
        TRUE = "true"
        UNTIL = "until"
        WHILE = "while"
        REGION = "REGION"
        ENDREGION = "ENDREGION"

        SHEBANG = "#!"
        SHEBANG_CONTENT = "SHEBANG_CONTENT"
        ELLIPSIS = "..."
        CONCAT = ".."
        EQ = "=="
        GE = ">="
        LE = "<="
        NE = "~="
        MINUS = "-"
        PLUS = "+"
        MULT = "*"
        MOD = "%"
        DIV = "/"
        ASSIGN = "="
        GT = ">"
        LT = "<"
        LPAREN = "("
        RPAREN = ")"
        LBRACK = "["
        RBRACK = "]"
        LCURLY = "{"
        RCURLY = "}"
        GETN = "#"
        COMMA = ","
        SEMI = ";"
        COLON = ":"
        DOT = "."
        EXP = "^"

        SHORT_COMMENT="SHORT_COMMENT"
        DOC_COMMENT="DOC_COMMENT"
        BLOCK_COMMENT="BLOCK_COMMENT"
        ID="regexp:[A-Za-z_][A-Za-z0-9_]*"
        NUMBER="NUMBER"
        STRING = "STRING"

        //lua 5.3
        DOUBLE_COLON = "::"
        GOTO = "goto"
        BIT_OR = "|"
        BIT_AND = "&"
        BIT_LTLT = "<<"
        BIT_RTRT = ">>"
        BIT_TILDE = "~"
        DOUBLE_DIV = "//"
  ]

  hooks("localDef|assignStat|tableField") = [leftBinder="MY_LEFT_COMMENT_BINDER" rightBinder="MY_RIGHT_COMMENT_BINDER"]
  hooks("globalFuncDef|localFuncDef|classMethodDef") = [leftBinder="MY_LEFT_COMMENT_BINDER"]
  hooks("fieldList") = [leftBinder="GREEDY_LEFT_BINDER" rightBinder="GREEDY_RIGHT_BINDER"]
  extends(".*Expr") = expr
  extends(".*Stat") = statement
  hooks(".*Stat") = [leftBinder="MY_LEFT_COMMENT_BINDER"]
}

luaFile ::= shebang_line? stat_semi*

shebang_line ::= SHEBANG SHEBANG_CONTENT

private stat_semi ::= stat_impl ';'?
block ::= stat_semi* (lastStat ';'?)?
private lazy_block ::= <<lazyBlock>>

fake statement ::= () {
    implements = "com.tang.intellij.lua.psi.LuaCommentOwner"
    methods = [getComment]
}
fake exprStat ::= expr
private stat_impl ::=
    callStat |
    assignStat |
    doStat |
    whileStat |
    repeatStat |
    ifStat |
    forAStat |
    forBStat |
    defStat |
    lastStat |
    labelStat |
    gotoStat |
    uncompletedStat {
        recoverWhile = stat_recover
    }
private stat_recover ::= !(ID
    | ',' | ';'
    | 'local' | 'do' | 'while' | 'repeat' | 'function' | 'if' | 'for' | 'return' | break
    | nil | true | false | STRING | NUMBER | '::' | 'goto'
    | unaryOp)

assignStat ::= varList '=' exprList {
    pin = 2
    implements = [
        "com.tang.intellij.lua.psi.LuaDeclaration"
    ]
    methods = [assign = 'ASSIGN' varExprList = '/exprList[0]' valueExprList = '/exprList[1]']
}
doStat ::= 'do' lazy_block 'end' {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaIndentRange"
    ]
}
whileStat ::= 'while' expr 'do' lazy_block 'end' {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaLoop"
        "com.tang.intellij.lua.psi.LuaIndentRange"
    ]
}
repeatStat ::= 'repeat' lazy_block 'until' expr {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaLoop"
        "com.tang.intellij.lua.psi.LuaIndentRange"
    ]
    methods = [
        until = 'UNTIL'
    ]
}
ifStat ::= 'if' expr 'then' lazy_block ('elseif' expr 'then' lazy_block)* ('else' lazy_block)? 'end' {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaIndentRange"
    ]
}
forAStat ::= 'for' paramNameDef '=' expr ',' expr (',' expr)? 'do' lazy_block 'end' {
    pin = 3
    methods = [getParamNameDefList]
    implements = [
        "com.tang.intellij.lua.psi.LuaParametersOwner"
        "com.tang.intellij.lua.psi.LuaLoop"
        "com.tang.intellij.lua.psi.LuaIndentRange"
    ]
}
forBStat ::= 'for' parList 'in' exprList 'do' lazy_block 'end' {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaParametersOwner"
        "com.tang.intellij.lua.psi.LuaLoop"
        "com.tang.intellij.lua.psi.LuaIndentRange"
    ]
}
labelStat ::= '::' ID '::' { pin = 1 }
gotoStat ::= 'goto' ID { pin = 1 }
private defStat ::= classMethodDef | globalFuncDef | localFuncDef | localDef
callStat ::= funcCallExpr { extends = exprStat }
uncompletedStat ::= expr { extends = exprStat }

globalFuncDef ::= 'function' ID funcBody {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaFuncBodyOwner"
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.tang.intellij.lua.psi.LuaGlobal"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    methods = [
        getComment
        getPresentation
        getParamNameDefList
        getNameIdentifier
        setName
        getName
        getTextOffset
        toString
        guessReturnTypeSet
        getParams
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaGlobalFuncStub"
}

private checkFuncPrefix ::= ID ('.' | ':') ID

left funcPrefixRef ::= '.' <<repeat checkFuncPrefix 1>> ID { extends = indexExpr elementType = indexExpr }
classMethodName ::=  nameExpr (funcPrefixRef)* (('.' ID) | (':' ID)) {
    pin = 3
    methods = [
        dot = 'DOT'
        colon = 'COLON'
    ]
}
classMethodDef ::= 'function' classMethodName funcBody {
    pin = 2
    implements = [
        "com.tang.intellij.lua.psi.LuaFuncBodyOwner"
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.tang.intellij.lua.psi.LuaClassMember"
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaClassMethodStub"
    methods = [
        getComment
        getClassType
        getParamNameDefList
        getNameIdentifier
        setName
        getName
        getTextOffset
        toString
        guessReturnTypeSet
        getParams
        isStatic
        getPresentation
    ]
}

localFuncDef ::= 'local' 'function' ID funcBody {
    pin = 2
    implements = [
        "com.tang.intellij.lua.psi.LuaFuncBodyOwner"
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.tang.intellij.lua.psi.LuaStatement"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    methods = [
        getComment
        getParamNameDefList
        getNameIdentifier
        setName
        getName
        getTextOffset
        getUseScope
        guessReturnTypeSet
        getParams
    ]
}
localDef ::= 'local' nameList ('=' exprList)? {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.tang.intellij.lua.psi.LuaStatement"
    ]
    methods = [getComment assign = 'ASSIGN']
}

returnStat ::= return exprList?
breakStat ::= break
private lastStat ::= returnStat | breakStat

varList ::= varExpr (',' varExpr)* { extends = exprList }

nameList ::= nameDef (',' nameDef)*

exprList ::= (expr ',')* expr { methods = [guessTypeAt] }

literalExpr ::= nil | false | true | NUMBER | STRING | "..."

valueExpr ::= varExpr | literalExpr | closureExpr | tableExpr | parenExpr

// 最终产生 callExpr | indexExpr | nameExpr
private varExpr ::= callOrIndexExpr | nameExpr

expr ::=
    orExpr |
    andExpr |
    conditionalExpr |
    bitOrExpr |
    bitTildeExpr |
    bitAndExpr |
    bitMoveExpr |
    concatExpr |
    addExpr |
    mulExpr |
    unaryExpr |
    expExpr |
    valueExpr {
    mixin = "com.tang.intellij.lua.psi.impl.LuaExprMixin"
    implements = [
        "com.tang.intellij.lua.psi.LuaExpression"
    ]
}

parenExpr ::= '(' expr ')' { pin = 1 }
unaryExpr ::= unaryOp expr { pin = 1 }

fake binaryExpr ::= ()
orExpr ::= expr 'or' expr { elementType = binaryExpr }
andExpr ::= expr 'and' expr { elementType = binaryExpr }
conditionalExpr ::= expr ('<'|'>'|'<='|'>='|'~='|'==') expr { elementType = binaryExpr }
bitOrExpr ::= expr '|' expr { elementType = binaryExpr }
bitTildeExpr ::= expr '~' expr { elementType = binaryExpr }
bitAndExpr ::= expr '&' expr { elementType = binaryExpr }
bitMoveExpr ::= expr ('<<'|'>>') expr { elementType = binaryExpr }
concatExpr ::= expr '..' expr { elementType = binaryExpr }
addExpr ::= expr ('+'|'-') expr { elementType = binaryExpr }
mulExpr ::= expr ('*'|'/'|'//'|'%') expr { elementType = binaryExpr }
expExpr ::= expr '^' expr { elementType = binaryExpr }

left indexExpr ::= '[' expr ']' | '.' ID | ':' ID {
    pin(".*") = 1
    methods = [
        getNameIdentifier
        setName
        getName
        getTextOffset
        getPresentation
        toString

        guessPrefixType
        guessValueType

        dot = 'DOT'
        colon = 'COLON'
    ]
    implements = [
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaIndexStub"
    mixin = "com.tang.intellij.lua.psi.impl.LuaIndexExprMixin"
}

arg_expr_list ::= (expr ',')* (expr |& ')') { extends = exprList elementType = exprList }
args ::=  '(' (arg_expr_list)? ')' | tableExpr | STRING { pin(".*") = 1 }
left callExpr ::= args {
    methods = [
        guessPrefixType
        resolveFuncBodyOwner
        getFirstStringArg

        isStaticMethodCall
        isMethodCall
        isFunctionCall
    ]
}

private suffixExpr ::= indexExpr | callExpr
private prefixExpr ::= parenExpr | nameExpr | literalExpr | tableExpr
// 最终产生 callExpr | indexExpr
private callOrIndexExpr ::= prefixExpr suffixExpr+
// callExpr
private funcCallExpr ::= callOrIndexExpr <<checkType 'CALL_EXPR'>>

closureExpr ::= 'function' funcBody {
    pin = 2
    implements = "com.tang.intellij.lua.psi.LuaFuncBodyOwner"
    methods = [getParamNameDefList guessReturnTypeSet getParams]
}

funcBody ::= '(' (parList)? ')' lazy_block? 'end' {
    pin = 3
    implements = [
        "com.tang.intellij.lua.psi.LuaIndentRange"
    ]
}

private parList ::= paramNameDef (',' paramNameDef)* (',' '...')? | '...' { recoverWhile = parList_recover }
private parList_recover ::= !(')'|'in')

tableExpr ::= '{' fieldList '}' {
    pin = 1
    methods = [findField toString]
    implements = [
        "com.tang.intellij.lua.psi.LuaIndentRange"
    ]
    mixin = "com.tang.intellij.lua.psi.impl.LuaTableExprMixin"
    stubClass = "com.tang.intellij.lua.stubs.LuaTableStub"
}

private fieldList ::= (tableField (tableFieldSep tableField)* (tableFieldSep)?)?

private tableField1 ::= '[' expr ']' '=' expr { pin = 1 }
private tableField2 ::= ID '=' expr { pin = 2 }

tableField ::= tableField1 | tableField2 | expr {
    methods = [
        getNameIdentifier
        setName
        getName
        getTextOffset

        toString

        getFieldName
        getPresentation
        guessType
        getComment
        nameDef = "tableFieldNameDef"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaTableFieldStub"
    implements = [
        "com.tang.intellij.lua.psi.LuaClassField"
        "com.intellij.psi.PsiNameIdentifierOwner"
        "com.tang.intellij.lua.psi.LuaCommentOwner"
    ]
    recoverWhile = tableField_recover
}

private tableField_recover ::= !(tableFieldSep | '}' | '[')

tableFieldSep ::= ',' | ';'

binaryOp ::= '+' | '-' | '*' | '/' | '^' | '%' | '..' |
    '<' | '<=' | '>' | '>=' | '==' | '~=' |
    'and' | 'or'
    // lua5.3
    | '|' | '&' | '>>' | '<<' | '~' | '//'

unaryOp ::= '-' | 'not' | '#'
    // lua5.3
    | '~'

fake name ::= ID {
    implements = [
        "com.tang.intellij.lua.psi.LuaNamedElement"
    ]
    methods = [setName getName]
}
nameDef ::= ID {
    implements = ["com.tang.intellij.lua.psi.LuaTypeGuessable" "com.intellij.psi.PsiNameIdentifierOwner"]
    methods = [guessType getNameIdentifier getUseScope]
    extends = name
}
nameExpr ::= ID {
    implements = ["com.tang.intellij.lua.psi.LuaPsiElement"
        "com.intellij.psi.PsiNameIdentifierOwner" ]
    methods = [
        setName
        getName
        getNameIdentifier
        getPresentation
        getReferences

        toString
    ]
    mixin = "com.tang.intellij.lua.psi.impl.LuaNameExprMixin"
    stubClass = "com.tang.intellij.lua.stubs.LuaNameStub"
}
paramNameDef ::= ID {
    extends = nameDef
}