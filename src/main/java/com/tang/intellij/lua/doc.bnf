/*
 * Copyright (c) 2017. tangzx(love.tangzx@qq.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

{
  parserClass="com.tang.intellij.lua.comment.parser.LuaDocParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  implements="com.tang.intellij.lua.comment.psi.LuaDocPsiElement"

  psiClassPrefix="LuaDoc"
  psiImplClassSuffix="Impl"
  psiPackage="com.tang.intellij.lua.comment.psi"
  psiImplPackage="com.tang.intellij.lua.comment.psi.impl"
  psiImplUtilClass="com.tang.intellij.lua.comment.psi.LuaDocPsiImplUtilKt"

  parserUtilClass="com.tang.intellij.lua.psi.LuaParserUtil"
  elementTypeHolderClass="com.tang.intellij.lua.comment.psi.LuaDocTypes"
  elementTypeClass="com.tang.intellij.lua.comment.psi.LuaDocElementType"
  elementTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinition.createDocType"
  tokenTypeClass="com.tang.intellij.lua.comment.psi.LuaDocTokenType"
  extends(".*ty") = 'ty'

  tokens = [
        ID="regexp:[A-Za-z0-9_]+"
        AT = '@'
        SHARP = '#'
        EQ = '='
        COMMA = ','
        EXTENDS = ':'
        OR = '|'
        LPAREN = "("
        RPAREN = ")"
        ARR = '[]'
        FUNCTION = 'function'
        TAG_NAME = 'TAG_NAME'
        TAG_PARAM = 'param'
        OPTIONAL = 'optional'
        TAG_RETURN = 'return'
        PROTECTED = 'protected'
        PUBLIC = 'public'
        CLASS = 'class'
        FIELD = 'field'
        DASHES = 'DASHES'
        STRING_BEGIN = 'STRING_BEGIN'
        STRING = 'STRING'
        TYPE = 'type'
  ]
}

doc ::= (DASHES after_dash?)*

private after_dash ::= doc_item | STRING //{ recoverWhile=after_dash_recover }
//private after_dash_recover ::= !(DASHES)
private doc_item ::= '@' (param_def | return_def | tag_def | class_def | field_def | type_def)

ty ::= function_ty | arr_ty | general_ty {
    implements = ['com.tang.intellij.lua.comment.psi.LuaDocType']
}

arr_ty ::= ty '[]' {
    methods = [getType]
}
general_ty ::= class_name_ref {
    methods = [getType]
}

function_param ::= ID ':' type_set
function_ty ::= function '(' function_param ')' (':' type_set)? {
    methods = [getType getReturnType]
}

//todo : remove # ,
type_set ::= '#'? ty ('|'? ty)*
access_modifier ::= "protected" | "public"

field_def ::= FIELD access_modifier ID type_set comment_string? {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaClassField"
        "com.tang.intellij.lua.comment.psi.LuaDocPsiElement"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaDocClassFieldStub"
    methods = [
        guessType
        getNameIdentifier
        setName
        getName
        getTextOffset
        getFieldName
        toString
        getPresentation
    ]
}

//--- tag
tag_value ::= ID
tag_def ::= TAG_NAME tag_value? comment_string? { pin = 1 }

//@param PARAM_NAME CLASS_REF
param_name_ref ::= ID {
    methods = [getReference]
}
param_def ::= TAG_PARAM OPTIONAL? param_name_ref type_set comment_string? {
    pin = 1
    methods = [guessType optional = 'OPTIONAL']
}

//--- return
type_list ::= type_set(',' type_set)*
return_def ::= TAG_RETURN type_list comment_string? {
    pin = 1
    methods = [resolveTypeAt]
}

class_name_ref ::= ID {
    methods = [getReference resolveType]
}
class_def ::= CLASS ID (EXTENDS class_name_ref)? comment_string? {
    pin = 2
    implements = [
        "com.tang.intellij.lua.comment.psi.LuaDocPsiElement"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaDocClassStub"
    methods = [
        getClassType
        
        getPresentation
        getNameIdentifier
        setName
        getName
        getTextOffset
        toString

        superClassNameRef = 'class_name_ref'
    ]
}

// @type CLASS_REF
type_def ::= TYPE type_set comment_string? {
    pin = 1
    stubClass = "com.tang.intellij.lua.stubs.LuaDocTyStub"
    methods = [guessType]
}

comment_string ::= STRING_BEGIN STRING? {
    pin = 1
    methods = [
        string = "STRING"
    ]
}