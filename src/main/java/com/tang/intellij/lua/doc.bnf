/*
 * Copyright (c) 2017. tangzx(love.tangzx@qq.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

{
  parserClass="com.tang.intellij.lua.comment.parser.LuaDocParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  implements="com.tang.intellij.lua.comment.psi.LuaDocPsiElement"

  psiClassPrefix="LuaDoc"
  psiImplClassSuffix="Impl"
  psiPackage="com.tang.intellij.lua.comment.psi"
  psiImplPackage="com.tang.intellij.lua.comment.psi.impl"
  psiImplUtilClass="com.tang.intellij.lua.comment.psi.LuaDocPsiImplUtilKt"

  parserUtilClass="com.tang.intellij.lua.psi.LuaParserUtil"
  elementTypeHolderClass="com.tang.intellij.lua.comment.psi.LuaDocTypes"
  elementTypeClass="com.tang.intellij.lua.comment.psi.LuaDocElementType"
  elementTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinitionKt.createDocType"
  tokenTypeClass="com.tang.intellij.lua.comment.psi.LuaDocTokenType"
  extends(".*ty") = 'ty'

  tokens = [
        ID="regexp:[A-Za-z0-9_]+"
        AT = '@'
        SHARP = '#'
        EQ = '='
        COMMA = ','
        EXTENDS = ':'
        OR = '|'
        GT = ">"
        LT = "<"
        LPAREN = "("
        RPAREN = ")"
        ARR = '[]'
        FUN = 'fun'
        TAG_NAME = 'TAG_NAME'
        TAG_PARAM = 'param'
        OPTIONAL = 'optional'
        TAG_RETURN = 'return'
        PRIVATE = 'private'
        PROTECTED = 'protected'
        PUBLIC = 'public'
        CLASS = 'class'
        FIELD = 'field'
        DASHES = 'DASHES'
        STRING_BEGIN = 'STRING_BEGIN'
        STRING = 'STRING'
        TYPE = 'type'
        LANGUAGE = 'language'
        OVERLOAD = 'overload'
        MODULE = 'module'
  ]
}

doc ::= (DASHES after_dash?)*

private after_dash ::= doc_item | STRING { recoverWhile=after_dash_recover }
private after_dash_recover ::= !(DASHES)
private doc_item ::= '@' (param_def | return_def | class_def | field_def | type_def | lan_def | overload_def | tag_def | access_modifier)

ty ::= union_ty | function_ty | generic_ty | arr_ty | general_ty | par_ty {
    implements = ['com.tang.intellij.lua.comment.psi.LuaDocType']
}

arr_ty ::= ty '[]' {
    methods = [getType]
}
general_ty ::= class_name_ref {
    methods = [getType]
}
par_ty ::= '(' ty ')' {
    pin = 1
    methods = [getType]
}

private generic_param_list ::= (ty ',')* ty
generic_ty ::= general_ty '<' generic_param_list '>' {
    methods = [getType]
    pin = 2
}

function_param ::= ID ':' ty {
    pin = 1
}
private function_param_list::= (function_param ',')* (function_param |& ')')
function_ty ::= fun '(' function_param_list ')' (':' type_list)? {
    methods = [getType getReturnType]
    pin = 1
}

union_ty ::= ty ('|' ty)+ {
    methods = [getType]
}
access_modifier ::= PRIVATE | PUBLIC | PROTECTED

field_def ::= FIELD access_modifier? ID ty comment_string? {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaClassField"
        "com.tang.intellij.lua.comment.psi.LuaDocPsiElement"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaDocClassFieldStub"
    methods = [
        guessType
        guessParentType
        getVisibility
        getNameIdentifier
        setName
        getName
        getTextOffset
        getFieldName
        toString
        getPresentation
    ]
}

//--- tag
tag_value ::= ID
tag_def ::= TAG_NAME tag_value? comment_string? { pin = 1 }

//@overload
overload_def ::= OVERLOAD function_ty { pin = 1 }

//@language json
lan_def ::= LANGUAGE ID { pin = 1 }

//@param PARAM_NAME CLASS_REF
param_name_ref ::= ID {
    methods = [getReference]
}
param_def ::= TAG_PARAM OPTIONAL? param_name_ref ty comment_string? {
    pin = 1
    methods = [getType optional = 'OPTIONAL']
}

//--- return
type_list ::= ty(',' ty)*
return_def ::= TAG_RETURN type_list comment_string? {
    pin = 1
    methods = [resolveTypeAt]
}

class_name_ref ::= ID {
    methods = [getReference resolveType]
}
class_def ::= (CLASS|MODULE) ID (EXTENDS class_name_ref)? comment_string? {
    pin = 2
    implements = [
        "com.tang.intellij.lua.comment.psi.LuaDocPsiElement"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaDocClassStub"
    methods = [
        getType
        
        getPresentation
        getNameIdentifier
        setName
        getName
        getTextOffset
        toString

        superClassNameRef = 'class_name_ref'
        module = 'MODULE'
    ]
}

// @type CLASS_REF
type_def ::= TYPE ty comment_string? {
    pin = 1
    stubClass = "com.tang.intellij.lua.stubs.LuaDocTyStub"
    methods = [getType]
}

comment_string ::= STRING_BEGIN STRING? {
    pin = 1
    methods = [
        string = "STRING"
    ]
}