/*
 * Copyright (c) 2017. tangzx(love.tangzx@qq.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
	id 'org.jetbrains.intellij' version "0.2.18"
	id "org.jetbrains.kotlin.jvm" version "1.2.31"
}

dependencies {
	compile fileTree(dir: "libs", includes: ["*.jar"])
}

sourceSets {
	main {
		java.srcDirs 'gen'
		resources {
			exclude "debugger/**"
		}
	}
}

apply from: "patchs/patch.gradle"

// Appveyor CI
if (System.env['APPVEYOR']) {
	version = System.env['APPVEYOR_BUILD_VERSION']
}

intellij {
	type 'IU'
	updateSinceUntilBuild false
	downloadSources false
	version = "IC-182.2371.4"
	localPath System.env['IDEA_HOME']
}

def RES_DIR = "src/main/resources"

task copyAttachDebugger(dependsOn: ["debugger:attach:windows:buildProduct"], type: Copy) {
	from(fileTree(dir: 'debugger/attach/windows/build/', includes: ['x64/**', 'x86/**']))
	destinationDir file("${RES_DIR}/debugger/windows")
}

buildPlugin.dependsOn(copyAttachDebugger)
buildPlugin.from(fileTree(dir: "${RES_DIR}", includes: ['debugger/**'])) {
	into "/${project.name}/classes/"
}

publishPlugin {
	username System.env['JETBRAINS_USERNAME']
	password System.env['JETBRAINS_PASSWORD']
	channels 'EAP'
}